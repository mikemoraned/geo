area := "edinburgh"
paths := "1000"
seed := "1"
profile := "pedestrian" # or "auto"
config_base := "../config"
output_base := "../output"
overturemaps_base := "/Volumes/PRO-G40/OvertureMaps/data/release/2025-05-21.0"

water:
    cargo run --release --bin water -- \
        --overturemaps {{overturemaps_base}} \
        --area {{config_base}}/{{area}}.toml \
        --water {{output_base}}/{{area}}/water.geojson

samples:
    mkdir -p {{output_base}}/{{area}}
    cargo run --release --bin sample -- \
        --overturemaps {{overturemaps_base}} \
        --area {{config_base}}/{{area}}.toml \
        --bounds {{output_base}}/{{area}}/bounds.geojson \
        --mask {{output_base}}/{{area}}/bounds_masked.geojson \
        --starts {{output_base}}/{{area}}/sample.{{paths}}.starts.geojson \
        --ends {{output_base}}/{{area}}/sample.{{paths}}.ends.geojson \
        --paths {{paths}} \
        --seed {{seed}}

routes:
    cargo run --release --bin routes -- \
        --overturemaps {{overturemaps_base}} \
        --area {{config_base}}/{{area}}.toml \
        --starts {{output_base}}/{{area}}/sample.{{paths}}.starts.geojson \
        --ends {{output_base}}/{{area}}/sample.{{paths}}.ends.geojson \
        --geojson {{output_base}}/{{area}}/routes_{{profile}}.{{paths}}.geojson \
        --profile {{profile}}

regions:
    cargo run --release --bin find_regions -- \
        --background {{output_base}}/{{area}}/bounds.geojson \
        --borders {{output_base}}/{{area}}/routes_{{profile}}.{{paths}}.geojson \
        --borders {{output_base}}/{{area}}/water.geojson \
        --stage-template {{output_base}}/{{area}}/routes_{{profile}}.{{paths}}.STAGE_NAME.png \
        --regions {{output_base}}/{{area}}/regions_{{profile}}.{{paths}}.geojson

layout:
    cargo run --release --bin layout -- \
        --regions {{output_base}}/{{area}}/regions_{{profile}}.{{paths}}.geojson \
        --bin-type guillotine \
        --layout {{output_base}}/{{area}}/layout_{{profile}}.{{paths}}.guillotine.png

    cargo run --release --bin layout -- \
        --regions {{output_base}}/{{area}}/regions_{{profile}}.{{paths}}.geojson \
        --bin-type max-rects \
        --layout {{output_base}}/{{area}}/layout_{{profile}}.{{paths}}.max-rects.png

regions_layout: regions layout
    
all: water samples routes regions_layout

areas_all_water:
    just --set area hamburg water
    just --set area edinburgh water
    just --set area jerusalem water
    just --set area new_york water
    just --set area paris water
    just --set area amsterdam water
    just --set area berlin water

areas_all:
    just --set area hamburg --set profile pedestrian all
    just --set area hamburg --set profile auto all

    just --set area edinburgh --set profile pedestrian all
    just --set area edinburgh --set profile auto all

    just --set area jerusalem --set profile pedestrian all
    just --set area jerusalem --set profile auto all

    just --set area new_york --set profile pedestrian all
    just --set area new_york --set profile auto all

    just --set area paris --set profile pedestrian all
    just --set area paris --set profile auto all

    just --set area amsterdam --set profile pedestrian all
    just --set area amsterdam --set profile auto all

    just --set area berlin --set profile pedestrian all
    just --set area berlin --set profile auto all

