area := "edinburgh"
paths := "1000"
seed := "1"
profile := "pedestrian" # or "auto"

samples:
    cargo run --release --bin sample -- \
        --area data/{{area}}/area.toml \
        --starts data/{{area}}/sample.{{paths}}.starts.geojson \
        --ends data/{{area}}/sample.{{paths}}.ends.geojson \
        --paths {{paths}} \
        --seed {{seed}}

routes:
    cargo run --release --bin routes -- \
        --starts data/{{area}}/sample.{{paths}}.starts.geojson \
        --ends data/{{area}}/sample.{{paths}}.ends.geojson \
        --geojson data/{{area}}/routes_{{profile}}.{{paths}}.geojson \
        --profile {{profile}}

regions:
    cargo run --release --bin find_regions -- \
        --routes data/{{area}}/routes_{{profile}}.{{paths}}.geojson \
        --stage-template data/{{area}}/routes_{{profile}}.{{paths}}.STAGE_NAME.png \
        --regions data/{{area}}/regions_{{profile}}.{{paths}}.geojson

layout:
    cargo run --release --bin layout -- \
        --regions data/{{area}}/regions_{{profile}}.{{paths}}.geojson \
        --bin-type guillotine \
        --layout data/{{area}}/layout_{{profile}}.{{paths}}.guillotine.png

    cargo run --release --bin layout -- \
        --regions data/{{area}}/regions_{{profile}}.{{paths}}.geojson \
        --bin-type max-rects \
        --layout data/{{area}}/layout_{{profile}}.{{paths}}.max-rects.png

regions_layout: regions layout
    
all: samples routes regions_layout
