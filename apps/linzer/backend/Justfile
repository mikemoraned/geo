area := "edinburgh"
paths := "1000"
seed := "1"
profile := "pedestrian" # or "auto"

samples:
    cargo run --release --bin sample -- \
        --area config/{{area}}.toml \
        --starts output/{{area}}/sample.{{paths}}.starts.geojson \
        --ends output/{{area}}/sample.{{paths}}.ends.geojson \
        --paths {{paths}} \
        --seed {{seed}}

routes:
    cargo run --release --bin routes -- \
        --starts output/{{area}}/sample.{{paths}}.starts.geojson \
        --ends output/{{area}}/sample.{{paths}}.ends.geojson \
        --geojson output/{{area}}/routes_{{profile}}.{{paths}}.geojson \
        --profile {{profile}}

regions:
    cargo run --release --bin find_regions -- \
        --routes output/{{area}}/routes_{{profile}}.{{paths}}.geojson \
        --stage-template output/{{area}}/routes_{{profile}}.{{paths}}.STAGE_NAME.png \
        --regions output/{{area}}/regions_{{profile}}.{{paths}}.geojson

layout:
    cargo run --release --bin layout -- \
        --regions output/{{area}}/regions_{{profile}}.{{paths}}.geojson \
        --bin-type guillotine \
        --layout output/{{area}}/layout_{{profile}}.{{paths}}.guillotine.png

    cargo run --release --bin layout -- \
        --regions output/{{area}}/regions_{{profile}}.{{paths}}.geojson \
        --bin-type max-rects \
        --layout output/{{area}}/layout_{{profile}}.{{paths}}.max-rects.png

regions_layout: regions layout
    
all: samples routes regions_layout
